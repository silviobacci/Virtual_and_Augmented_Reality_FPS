#define WORLD_FLOOR_TILING 		30
#define WORLD_WALL_HEIGHT 		1
#define WORLD_LADDER_HEIGHT		0.2
#define WORLD_SKY_HEIGHT	 	0

#define	N_STEPS					WORLD_DEFAULT_DIM / WORLD_LADDER_HEIGHT

#define UP						1
#define DOWN					2
#define R						3
#define L						4

#define	WORLD_MAP_FILENAME		"map.txt"

#define	WORLD_DEFAULT_DIM		1

#define	WORLD_SUN_POSITION		[30.0, 10.0, 30.0]
#define	WORLD_SUN_DIFFUSE		[255, 255, 255, 255] / 255.0

#define	WORLD_SKY_COLOR			[255, 255, 255, 255] / 255.0
#define	WORLD_SKY_MESH			"sky/sky.aam"

#define	WORLD_FLOOR_COLOR		[255, 255, 255, 255] / 255.0
#define	WORLD_FLOOR_IMAGE		"textures/grass.jpg"

#define	WORLD_WALL_COLOR		[255, 255, 255, 255] / 255.0
#define	WORLD_WALL_IMAGE		"textures/wall.jpg"

#define	WORLD_TREE_COLOR		[255, 255, 255, 255] / 255.0
#define	WORLD_TREE_IMAGE		"textures/tree.png"

var my_world;
var my_gadget;

class world{
	var mesh_sky;
	var material_sky;
	var texture_sky;
	
	var mesh_floor;
	var material_floor;
	var texture_floor;
	
	var mesh_wall;
	var material_wall;
	var texture_wall;
	
	var mesh_ladder;
	var material_ladder;
	var texture_ladder;
	
	var map;
	var matrix_level;
	var width;
	var height;
	
	var obj_array;
	var wall_array;
	var ladder_array;
	var wall;
	var sun;
	
	init();
	draw();
	
	find_wall_height(height);
	create_map();
	create_sun();
	create_sky();
	create_floor();
	create_ladder(height_scale, dir, i, j);
	create_wall(height_scale, i, j);
	create_tree(height_scale, i, j);
};

function world::find_wall_height(height) {
	switch(height) {
		case "x":
		case "z":
			return 0;
		case "1":
		case "a":
		case "g":
		case "o":
		case "w":
			return 1;
		case "2":
		case "3":
		case "b":
		case "h":
		case "p":
			return 2;
		case "4":
		case "c":
		case "i":
		case "q":
			return 3;
		case "5":
		case "6":
		case "d":
		case "l":
		case "r":
			return 4;
		case "7":
		case "e":
		case "m":
		case "s":
			return 5;
		case "8":
		case "9":
		case "f":
		case "n":
		case "t":
			return 6;
		default:
			return val(height);
	}	
}

function world::create_map() {
	var array_matrix = LoadFile(WORLD_MAP_FILENAME);
	height = len(array_matrix) - 1;
	width = len(array_matrix[0]);
	
	wall_array = array(0);
	matrix_level = array(height * N_STEPS);
	map = array(height);
	
	for (var i = 0; i < height; i++){
		map[i] = array(width);
		for(var k = 0; k < N_STEPS; k++)
			matrix_level[i * N_STEPS + k] = array(width * N_STEPS);

		for (var j = 0; j < width; j++){
			map[i][j] = find_wall_height(array_matrix[i][j]);
			
			if(map[i][j] > 0)	
				switch(array_matrix[i][j]) {
					case "1":
					case "2":
					case "4":
					case "5":
					case "7":
					case "8":
						create_ladder(map[i][j] - 1, UP, i, j);
						for(var k = 0; k < N_STEPS; k++)
							for(var v = 0; v < N_STEPS; v++)
								matrix_level[i * N_STEPS + k][j * N_STEPS + v] = find_wall_height(array_matrix[i+1][j]) + (N_STEPS - 1 - k) / ToFloat(N_STEPS);
						break;
					case "a":
					case "b":
					case "c":
					case "d":
					case "e":
					case "f":
						create_ladder(map[i][j] - 1, DOWN, i, j);
						for(var k = 0; k < N_STEPS; k++)
							for(var v = 0; v < N_STEPS; v++)
								matrix_level[i * N_STEPS + k][j * N_STEPS + v] = find_wall_height(array_matrix[i-1][j]) + k / ToFloat(N_STEPS);
						break;
					case "g":
					case "h":
					case "i":
					case "l":
					case "m":
					case "n":
						create_ladder(map[i][j] - 1, R, i, j);
						for(var k = 0; k < N_STEPS; k++)
							for(var v = 0; v < N_STEPS; v++)
								matrix_level[i * N_STEPS + k][j * N_STEPS + v] = find_wall_height(array_matrix[i][j+1]) + (N_STEPS - 1 - v) / ToFloat(N_STEPS);
						break;
					case "o":
					case "p":
					case "q":
					case "r":
					case "s":
					case "t":
						create_ladder(map[i][j] - 1, L, i, j);
						for(var k = 0; k < N_STEPS; k++)
							for(var v = 0; v < N_STEPS; v++)
								matrix_level[i * N_STEPS + k][j * N_STEPS + v] = find_wall_height(array_matrix[i][j-1]) + v / ToFloat(N_STEPS);
						break;
					case "3":
					case "6":
					case "9":
						create_wall(map[i][j], i, j);
						for(var k = 0; k < N_STEPS; k++)
							for(var v = 0; v < N_STEPS; v++)
								matrix_level[i * N_STEPS + k][j * N_STEPS + v] = map[i][j];
						break;
					case "w":
						create_tree(map[i][j], i, j);
						for(var k = 0; k < N_STEPS; k++)
							for(var v = 0; v < N_STEPS; v++) 
								matrix_level[i * N_STEPS + k][j * N_STEPS + v] = map[i][j];
						break;
				}
			else {
				switch(array_matrix[i][j]) {
					case "z":
						my_gadget.add_therapy_zone([j, map[i][j] - 1, i]);
						break;
					case "x":
						my_gadget.add_ammunition_zone([j, map[i][j] - 1, i]);
						break;
				}
				for(var k = 0; k < N_STEPS; k++)
							for(var v = 0; v < N_STEPS; v++) 
								matrix_level[i * N_STEPS + k][j * N_STEPS + v] = map[i][j];
			}
		}
	}
}

function world::create_sun() {
	sun = CVmLight();
	sun.SetPosition(WORLD_SUN_POSITION);
	sun.SetDiffuse(1, 1, 1);
	sun.SetParameter(GL_LINEAR_ATTENUATION, 0);
	sun.Disable();
}

function world::create_sky() {
	mesh_sky = CVmNewMesh(WORLD_SKY_MESH);
	mesh_sky.translate(width / 2.0, WORLD_SKY_HEIGHT, height / 2.0);
}

function world::create_floor() {
	mesh_floor = CVmNewMesh(VRP_BOX);
	mesh_floor.scale(width, WORLD_DEFAULT_DIM, height);
	mesh_floor.translate(0, -WORLD_DEFAULT_DIM, 0);
	
	material_floor = CVmMaterial();
	material_floor.diffuse = WORLD_FLOOR_COLOR;
	material_floor.ChangeTextureParams(VR_U_TILING, WORLD_FLOOR_TILING);
	material_floor.ChangeTextureParams(VR_V_TILING, WORLD_FLOOR_TILING);
	
	texture_floor = CVmTexture(WORLD_FLOOR_IMAGE);
	
	material_floor.SetTexture(texture_floor);
	mesh_floor.ForceMaterial(material_floor);
}

function world::create_ladder(height_scale, dir, i, j) {
	var ladder;
	var ladder_array = array(0);
	
	texture_ladder = CVmTexture(WORLD_WALL_IMAGE);
	
	mesh_ladder = array(WORLD_DEFAULT_DIM / WORLD_LADDER_HEIGHT);
	material_ladder = array(WORLD_DEFAULT_DIM / WORLD_LADDER_HEIGHT);
	
	for (var k = 0; k < WORLD_DEFAULT_DIM / WORLD_LADDER_HEIGHT; k++) {
		mesh_ladder[k] = CVmNewMesh(VRP_BOX);
		material_ladder[k] = CVmMaterial();
		material_ladder[k].SetTexture(texture_ladder);
		material_ladder[k].diffuse = WORLD_WALL_COLOR; 
			
		switch(dir) {
			case UP:
				material_ladder[k].ChangeTextureParams(VR_U_TILING, WORLD_WALL_HEIGHT);
				material_ladder[k].ChangeTextureParams(VR_V_TILING, WORLD_WALL_HEIGHT * height_scale + WORLD_DEFAULT_DIM - k * WORLD_LADDER_HEIGHT);
				mesh_ladder[k].scale(WORLD_DEFAULT_DIM, WORLD_WALL_HEIGHT * height_scale + WORLD_DEFAULT_DIM - k * WORLD_LADDER_HEIGHT, WORLD_LADDER_HEIGHT);
				mesh_ladder[k].ForceMaterial(material_ladder[k]);
				ladder = CVmObj(mesh_ladder[k]);
				ladder.SetPosition(j, 0, i + k * WORLD_LADDER_HEIGHT);
				break;
			case DOWN:
				material_ladder[k].ChangeTextureParams(VR_U_TILING, WORLD_WALL_HEIGHT);
				material_ladder[k].ChangeTextureParams(VR_V_TILING, WORLD_WALL_HEIGHT * height_scale + (k + 1) * WORLD_LADDER_HEIGHT);
				mesh_ladder[k].scale(WORLD_DEFAULT_DIM, WORLD_WALL_HEIGHT * height_scale + (k + 1) * WORLD_LADDER_HEIGHT, WORLD_LADDER_HEIGHT);
				mesh_ladder[k].ForceMaterial(material_ladder[k]);
				ladder = CVmObj(mesh_ladder[k]);
				ladder.SetPosition(j, 0, i + k * WORLD_LADDER_HEIGHT);
				break;
			case L:
				material_ladder[k].ChangeTextureParams(VR_U_TILING, WORLD_WALL_HEIGHT);
				material_ladder[k].ChangeTextureParams(VR_V_TILING, WORLD_WALL_HEIGHT * height_scale + (k + 1) * WORLD_LADDER_HEIGHT);
				mesh_ladder[k].scale(WORLD_LADDER_HEIGHT, WORLD_WALL_HEIGHT * height_scale + (k + 1) * WORLD_LADDER_HEIGHT, WORLD_DEFAULT_DIM);
				mesh_ladder[k].ForceMaterial(material_ladder[k]);
				ladder = CVmObj(mesh_ladder[k]);
				ladder.SetPosition(j + k * WORLD_LADDER_HEIGHT, 0, i);
				break;
			case R:
				material_ladder[k].ChangeTextureParams(VR_U_TILING, WORLD_WALL_HEIGHT);
				material_ladder[k].ChangeTextureParams(VR_V_TILING, WORLD_WALL_HEIGHT * height_scale + WORLD_DEFAULT_DIM - k * WORLD_LADDER_HEIGHT);
				mesh_ladder[k].scale(WORLD_LADDER_HEIGHT, WORLD_WALL_HEIGHT * height_scale + WORLD_DEFAULT_DIM - k * WORLD_LADDER_HEIGHT, WORLD_DEFAULT_DIM);
				mesh_ladder[k].ForceMaterial(material_ladder[k]);
				ladder = CVmObj(mesh_ladder[k]);
				ladder.SetPosition(j + k * WORLD_LADDER_HEIGHT, 0, i);
				break;
		}
						
		aadd(ladder_array, ladder);
	}
	
	switch(dir) {
		case UP:
			for (var k = 0; k < WORLD_DEFAULT_DIM / WORLD_LADDER_HEIGHT; k++)
				aadd(wall_array, ladder_array[k]);
			break;
		case DOWN:
			for (var k = WORLD_DEFAULT_DIM / WORLD_LADDER_HEIGHT - 1; k >= 0; k--)
				aadd(wall_array, ladder_array[k]);
			break;
		case R:
			for (var k = WORLD_DEFAULT_DIM / WORLD_LADDER_HEIGHT - 1; k >= 0; k--)
				aadd(wall_array, ladder_array[k]);
			break;
		case L:
			for (var k = 0; k < WORLD_DEFAULT_DIM / WORLD_LADDER_HEIGHT; k++)
				aadd(wall_array, ladder_array[k]);
			break;
	}
}

function world::create_wall(height_scale, i, j) {
	mesh_wall = CVmNewMesh(VRP_BOX);
	
	material_wall = CVmMaterial();
	material_wall.diffuse = WORLD_WALL_COLOR; 
	material_wall.ChangeTextureParams(VR_V_TILING, WORLD_WALL_HEIGHT * height_scale);
	material_wall.ChangeTextureParams(VR_U_TILING, WORLD_DEFAULT_DIM);
	
	texture_wall = CVmTexture(WORLD_WALL_IMAGE);
	
	material_wall.SetTexture(texture_wall);
	mesh_wall.ForceMaterial(material_wall);
	
	wall = CVmObj(mesh_wall);
	wall.SetScale(WORLD_DEFAULT_DIM, WORLD_WALL_HEIGHT * height_scale, WORLD_DEFAULT_DIM);
	wall.SetPosition(j, 0, i);
	
	aadd(wall_array, wall);
}

function world::create_tree(height_scale, i, j) {
	var billboard_tree = CVmBillboard();
	var material_tree = CVmMaterial();
	var texture_tree = CVmTexture(WORLD_TREE_IMAGE);
	material_tree.diffuse = [1, 1, 1, 1];
	material_tree.transparency = 0;
	billboard_tree.SetMaterial(material_tree);
	billboard_tree.SetTexture(texture_tree);
	billboard_tree.SetSize(WORLD_DEFAULT_DIM, 2 * WORLD_DEFAULT_DIM);
	billboard_tree.SetPosition(j, height_scale, i);
	aadd(wall_array, billboard_tree);
}

function world::init(){
	create_map();
	create_sun();
	create_sky();
	create_floor();
}

function world::draw(){
	mesh_sky.draw();
	mesh_floor.draw();
	
	foreach(var wall in wall_array)
		wall.draw();
}