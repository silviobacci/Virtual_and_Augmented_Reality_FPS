#define INFO_BOX_WIDTH			0.15
#define INFO_LP_HEIGHT			0.015

#define INFO_START_POINT_X		0.01

#define INFO_FONT_SIZE			10

#define INFO_COLOR				[244, 225, 174] / 255.0
#define INFO_BOX_COLOR			[139, 63, 51] / 255.0
#define INFO_TEXT_COLOR			[255, 255, 255] / 255.0
#define LP_GREEN_COLOR			[0, 255, 0] / 255.0
#define LP_ORANGE_COLOR			[0, 0, 255] / 255.0
#define LP_RED_COLOR			[255, 0, 0] / 255.0

var my_player_info;

class player_info{
	var player;
	var box_x;
	var box_y;
	var box_w;
	var box_h;
	var box;
	
	var lp_x;
	var lp_y;
	var lp;
	
	draw();
	update(p);
	
	draw_info_box();
	draw_player_id();
	draw_player_position();
	draw_gun_selected();
	draw_bullets_left();
	draw_life_points();	
	draw_lp_bar();
	
	get_inside();
	get_inside_text(displacement);
};

function player_info::update(p) {
	player = p;
	
	box_x = INFO_START_POINT_X;	
	box_y = (INFO_START_POINT_X + INFO_BOX_WIDTH) * resolution;
	lp_x = INFO_START_POINT_X + INFO_BOX_WIDTH + INFO_START_POINT_X;	
	lp_y = INFO_START_POINT_X * resolution + INFO_LP_HEIGHT;
}

function player_info::get_inside() {
	var tmp_box = array(4);
	tmp_box[0] = box[0] + box[2] * 0.05; 
	tmp_box[1] = box[1] - box[3] * 0.05; 
	tmp_box[2] = box[2] * 0.9; 
	tmp_box[3] = box[3] * 0.9;
	
	return tmp_box;
}

function player_info::get_inside_text(displacement) {
	var tmp_box = get_inside(box);
	tmp_box[0] += tmp_box[2] / 2.0; 
	tmp_box[1] -= tmp_box[3] / 2.0 - displacement * 0.01; 
	tmp_box[3] = 0.01;
	
	return tmp_box;
}

function  player_info::draw_player_id() {
	var tmp_box = get_inside_text(8);
	draw_box(tmp_box, "PLAYER ID: " + str(my_network.my_id), INFO_FONT_SIZE);
}

function  player_info::draw_player_position() {
	var tmp_box = get_inside_text(4);
	var position = player.get_position();
	draw_box(tmp_box, "GPS: " + str(floor(position.x)) + "," + str(floor(position.z)), INFO_FONT_SIZE);
}

function  player_info::draw_gun_selected() {
	var tmp_box = get_inside_text(0);
	draw_box(tmp_box, "GUN: " + player.current_weapon.name, INFO_FONT_SIZE);
}

function  player_info::draw_bullets_left() {
	var tmp_box = get_inside_text(-4);
	draw_box(tmp_box, "BULLETS: " + str(player.current_weapon.bullets_left) + " / " + str(player.current_weapon.n_bullets), INFO_FONT_SIZE);
}

function  player_info::draw_life_points() {
	var tmp_box = get_inside_text(-8);
	draw_box(tmp_box, "LIFE: " + str(my_players.array_player[my_players.my_index].life_points) + "%", INFO_FONT_SIZE);
}
	
function  player_info::draw_info_box() {
	ConsoleColor(INFO_COLOR);
	ConsoleFilledRect(box[0], box[1], box[2], box[3]);
	ConsoleColor(INFO_BOX_COLOR);
	var tmp_box = get_inside();
	ConsoleFilledRect(tmp_box[0], tmp_box[1], tmp_box[2], tmp_box[3]);
}

function  player_info::draw_lp_bar() {
	var my_lp = my_players.array_player[my_players.my_index].life_points;
	
	if(my_lp > 75)
		ConsoleColor(LP_GREEN_COLOR);
	else if(my_lp > 30)
		ConsoleColor(LP_ORANGE_COLOR);
	else
		ConsoleColor(LP_RED_COLOR);
		
	lp = [lp_x, lp_y, my_lp * (1 - lp_x - INFO_START_POINT_X) / 100, INFO_LP_HEIGHT];
	
	ConsoleFilledRect(lp[0], lp[1], lp[2], lp[3]);
}

function player_info::draw() {
	box = [box_x, box_y, INFO_BOX_WIDTH, INFO_BOX_WIDTH * resolution];
	draw_info_box();
	draw_player_id();
	draw_player_position();
	draw_gun_selected();
	draw_bullets_left();
	draw_life_points();	
	draw_lp_bar();
	ConsoleFinish();
}