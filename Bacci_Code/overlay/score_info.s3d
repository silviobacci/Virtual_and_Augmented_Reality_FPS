#define SCORE_BOX_WIDTH			0.15

#define SCORE_START_POINT_X		0.01

#define SCORE_FONT_SIZE			10

#define SCORE_COLOR				[244, 225, 174] / 255.0
#define SCORE_BOX_COLOR			[139, 63, 51] / 255.0
#define SCORE_TEXT_COLOR		[255, 255, 255] / 255.0

#define MAX_VISIBLE_SCORES		4
#define WINNER_SCORE			100

class score_info{
	var scores;
	var box;
	var box_x;
	var box_y;
	var box_w;
	var box_h;
	
	init();
	
	add_score(player_id, score);
	check_if_win(player_id, score, my_id);
	update(player_id, score, my_id);
	update_from_pdu(scores, my_id);
	
	sort_scores();
	get_inside();
	get_inside_text(displacement);
	draw_score_box();
	draw_score_title();
	draw_score();
	draw();
};

function score_info::init() {
	scores = array(0);
}

function score_info::sort_scores() {
	for (var i = len(scores) - 1; i > 0; i--)
		for (var j = 0; j < i; j++)
			if (scores[j][1] < scores[j + 1][1]) {
				var temp = scores[j + 1];
				scores[j + 1] = scores[j];
				scores[j] = temp;
			}
}

function score_info::add_score(player_id, score) {
	for(var i = 0; i < len(scores); i++) {
		if(scores[i][0] == player_id) {
			return;
		}
	}
	aadd(scores, [player_id, score]);
	sort_scores();
}

function score_info::check_if_win(player_id, score, my_id) {
	if(score >= WINNER_SCORE) {
		if(player_id == my_id)
			my_end_menu.winner = true;
		else
			my_end_menu.winner = false;
		my_end_menu.show_menu = true; 
		my_sound_player.play_end();
	}
}

function score_info::update(player_id, score, my_id) {
	for(var i = 0; i < len(scores); i++)
		if(scores[i][0] == player_id) {
			scores[i][1] += score;
			check_if_win(scores[i][0], scores[i][1], my_id);
		}
	sort_scores();
}

function score_info::update_from_pdu(new_scores, my_id) {
	for(var i = 0; i < len(scores); i++)
		for(var j = 2; j < len(new_scores); j++)
			if(new_scores[j][0] == scores[i][0] && new_scores[j][1] > scores[i][1]) {
				scores[i][1] = new_scores[j][i];
				check_if_win(scores[i][0], scores[i][1], my_id);
			}
	sort_scores();
}

function score_info::get_inside() {
	var tmp_box = array(4);
	tmp_box[0] = box[0] + box[2] * 0.05; 
	tmp_box[1] = box[1] - box[3] * 0.05; 
	tmp_box[2] = box[2] * 0.9;
	tmp_box[3] = box[3] * 0.9;
	
	return tmp_box;
}

function score_info::get_inside_text(displacement) {
	var tmp_box = get_inside(box);
	tmp_box[0] += tmp_box[2] / 2.0; 
	tmp_box[1] -= tmp_box[3] / 2.0 - displacement * 0.01; 
	tmp_box[3] = 0.01;
	
	return tmp_box;
}
	
function  score_info::draw_score_box() {
	ConsoleColor(SCORE_COLOR);
	ConsoleFilledRect(box[0], box[1], box[2], box[3]);
	ConsoleColor(SCORE_BOX_COLOR);
	var tmp_box = get_inside();
	ConsoleFilledRect(tmp_box[0], tmp_box[1], tmp_box[2], tmp_box[3]);
}

function  score_info::draw_score_title() {
	var tmp_box = get_inside_text(8);
	draw_box(tmp_box, "BEST SCORES", SCORE_FONT_SIZE);
}

function  score_info::draw_score() {
	var tmp_box;
	
	var min = len(scores) < MAX_VISIBLE_SCORES ? len(scores) : MAX_VISIBLE_SCORES;
	
	for(var i = 0; i < min; i++) {
		tmp_box = get_inside_text(4 - 4 * i);
		draw_box(tmp_box, str(i + 1) + ") PLAYER " + str(scores[i][0]) + ": " + str(scores[i][1]), SCORE_FONT_SIZE);
	}
}

function score_info::draw() {
	if(!my_end_menu.show_menu) {
		box_x = 1 - SCORE_START_POINT_X - SCORE_BOX_WIDTH;
		box_y = 1 - SCORE_START_POINT_X * resolution;
	}
	else {
		box_x = 0.5 - SCORE_BOX_WIDTH / 2;
		box_y = 0.5 + SCORE_BOX_WIDTH * resolution / 2;
	}
	
	box = [box_x, box_y, SCORE_BOX_WIDTH, SCORE_BOX_WIDTH * resolution];
	draw_score_box();
	draw_score_title();
	draw_score();
	ConsoleFinish();
}