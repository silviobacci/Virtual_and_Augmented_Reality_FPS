#define TIME_DELAY_MOUSE	300

var my_mouse;

class mouse_manager {
	var old_time;
	var clickable;
	var cursor_shape;
	var visible;
	var change_visible;
	var change_shape;
	
	init();
	update();
	draw();
	
	is_clicked();
	is_contained(box);
	
	change_to_hand();
	change_to_arrow();
	set_visible();
	set_invisible();
};

function mouse_manager::init() {
	cursor_shape = VR_CUR_ARROW;
	change_visible = false;
	change_shape = false;
	visible = true;
	clickable = true;
	old_time = 0;
}

function mouse_manager::update() {
	if(getTime() - old_time < TIME_DELAY_MOUSE)
		return;
	
	clickable = true;
	old_time = getTime();
}

function mouse_manager::draw() {
	if(change_visible) {
		ShowCursor(visible);
		ShowCursor(visible);
	}
	
	if(change_shape)
		SetCursorShape(cursor_shape);
		
	change_visible = false;
	change_shape = false;
}

function mouse_manager::change_to_hand() {
	if(cursor_shape == VR_CUR_HAND)
		return;
		
	cursor_shape = VR_CUR_HAND;
	change_shape = true;
}

function mouse_manager::change_to_arrow() {
	if(cursor_shape == VR_CUR_ARROW)
		return;
		
	cursor_shape = VR_CUR_ARROW;
	change_shape = true;
}

function mouse_manager::set_visible() {
	if(visible)
		return;
		
	visible = true;
	change_visible = true;
}

function mouse_manager::set_invisible() {
	if(!visible)
		return;
		
	visible = false;
	change_visible = true;
}

function mouse_manager::is_clicked() {
	if(!clickable)
		return false;
		
	if(mouse.ButtonL) {
		clickable = false;
		return true;	
	}
	
	return false;
}

function mouse_manager::is_contained(box) {
	var mouse_x = mouse.X / window_w;
	var mouse_y = 1 - mouse.Y / window_h;
	
	if(!(mouse_x >= box[0] - box[2] / 2.0 && mouse_x <= box[0] + box[2] / 2.0 && mouse_y >= box[1] - box[3] / 2.0 && mouse_y <= box[1] + box[3] / 2.0)) {
		change_to_arrow();
		return false;	
	}
	
	change_to_hand();
	return true;
}